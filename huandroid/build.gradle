apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

group = 'xyz.nulldev.huandroid'
version = "2.30.01"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27

        // HtmlUnitVersion + two digit port version
        versionCode version.replaceAll('\\.','') as Integer
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // javax.script
    api group: 'com.sun.phobos', name: 'jsr223-api', version: '1.0'

    // Proper httpclient support
    implementation "cz.msebera.android:httpclient:4.4.1.2"

    // Without httpmime, we need to specify logger + codec explicitly
    // Yes, I know there is an error, but whatever, it works
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.11'

    implementation(group: 'xalan', name: 'xalan', version: '2.7.2') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    implementation "org.apache.commons:commons-lang3:3.7"
    implementation "org.apache.commons:commons-text:1.3"
    // Replaced with cz.msebera httpclient
//    implementation "org.apache.httpcomponents:httpmime:4.5.5"
    implementation "net.sourceforge.htmlunit:htmlunit-core-js:2.28"
    implementation "net.sourceforge.htmlunit:neko-htmlunit:2.30"
    implementation "net.sourceforge.htmlunit:htmlunit-cssparser:1.0.0"
    implementation "commons-io:commons-io:2.6"
    implementation "commons-net:commons-net:3.6"
    implementation "org.eclipse.jetty.websocket:websocket-client:9.4.9.v20180320"

    implementation 'com.android.support:appcompat-v7:27.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError = false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    publish = true
    user = properties.getProperty('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = properties.getProperty('bintray.apikey') ?: System.getenv('BINTRAY_API_KEY')

    pkg {
        configurations = ['archives']
        
        repo = 'maven'
        name = 'HtmlUnit-Android'
        licenses = ['Apache-2.0', 'GPL-2.0']
        vcsUrl = 'https://github.com/null-dev/HtmlUnit-Android.git'
        publicDownloadNumbers = true
    }
}

bintrayUpload.dependsOn install
